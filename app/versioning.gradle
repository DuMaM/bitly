def getVersionCode() {
    def stdout = ''
    try {
        stdout = new ByteArrayOutputStream()
        exec {
            commandLine "git", "rev-list", "HEAD", "--count"
            standardOutput = stdout
        }

        return Integer.parseInt(stdout.toString().trim())
    } catch (exception) {
        println "Error: " + exception.getMessage()
        return -1
    }
}

def getVersionTag() {
    def stdout = ''
    try {
        stdout = new ByteArrayOutputStream()
        exec {
            commandLine "git", "describe", "--tags", "--dirty"
            standardOutput = stdout
        }

        return stdout.toString().trim()
    } catch (exception) {
        println "Error: " + exception.getMessage()
        return "0.0.1"
    }
}

// Export methods by turning them into closures
ext {
    getVersionTag = this.&getVersionTag
    getVersionCode = this.&getVersionCode
}
